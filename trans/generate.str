module generate

imports
	
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/MetaformaLang
  trans/constructors
  lib/editor-common.generated
  lib/compilation-library.generated
  libjava-front
  lib/editor-common.generated
  lib/index-library.generated
  lib/analysis-library.generated
  lib/analysis-library-internal.generated
  lib/compilation-library.generated
  lib/nbl-library.generated
	names
	desugar
	generate-indirect

 
strategies

 
 
 to-java: Group (g,mods) -> <map2str(to-java(|g))>mods
 
 to-java(|g): Mod(n) -> $[[g]_[n],]
 to-java: Group(n) -> $[[n](true),]  
 
 
 
rules // Transformation to java strings.










add-glue(|glue): e -> (glue,e) 
concat-glue(|glue): [head|tail] ->  $[[head][<map(conc-strings)><map(add-glue(|glue))>tail]]
concat-glue(|glue): [] -> $[]

to-java-stat: exp -> $[[<to-java>exp];]
where
	<debug(!"Compile stat: ")>exp

to-java: FuncCall(n,args) -> $[[<to-java>n]([<concat-glue(|",")><map(to-java)>args])]
to-java: ModHolderRef(g,m) -> $[Mod.[g]_[m]]
to-java: ModHolderRef(g) -> $[Group.[g]]  
to-java: FuncRef(n) -> n

to-java: OrientRef(o) -> $[Orientation.[o]]
to-java: MethodRef(obj,n) -> $[[<to-java>obj].[n]]

to-java-instrs: Instr(i,evhdlrs,stat*,postcon) -> $[ 
	[instr] 
	] 
where
	<debug>$[Instruction start [i]];
	<concat-strings><map(to-java-stat)>stat* => block;
	<to-java-stop(|i,block)>postcon => instr;
	<debug>$[Instruction end [i]]

// to-java-all: Start(Module(m),defs,decls) -> $[public class [m]Controller extends MfController implements ControllerInformationProvider {
// 	[<concat><map(to-java-def)>defs]
// 	}]

to-java-stop(|c,s): Wait(w) -> 
$[
	if (mfStateMngr.doWait([c]))  {
		[s]
		[mngr]
	}]
where
	<to-java-stop>w => mngr

to-java-stop(|c,s): Until(w) -> $[
	if (mfStateMngr.doUntil([c]))  {
		[s]
		[mngr]
	}]
where
	<to-java-stop>w => mngr

to-java-stop: Consensus() -> $[mfStateMngr.commitEnd();]

to-java-stop: e -> $[mfStateMngr.spend([e']);] 
	where
		not (<?Consensus()>e);
		<to-java>e => e'




mod-enum: Mod(groupname,name) -> $[[groupname]_[name]]
mod-enum: Group(groupname) -> $[[groupname](true)]




to-java: TryRegion(n,o,incs,excls) -> $[meta().tryRegion(new byte[brackets]{[<concat-glue(|",")><map(to-java)>incs]},new byte[brackets]{[<concat-glue(|",")><map(to-java)>excls]},[<to-java>n],[<to-java>o]);
]
where brackets := "[]"

to-java: OrientMatch(orients) -> $[[<concat-glue(|" || ")><map(orientref)>orients]
] 

orientref: OrientRef(r) -> $[mfStateMngr.getState().getOrientation(Orientation.[r]))]


to-java: Goto(s) -> $[mfStateMngr.nextOperation([<to-java>s]);]


to-java: StateRef(n) -> $[Sequence.[n]] 


to-java: BinOp (op,e1,e2) -> $[([<to-java>e1] [<to-java>op] [<to-java>e2])]
// where <debug>"Compile BinOp"

to-java: AndBit() -> $[ & ]
to-java: OrBit() -> $[ | ]

to-java: And() -> $[ && ]
to-java: Or() -> $[ || ]
to-java: Eq() -> $[ == ]
to-java: Neq() -> $[ != ]
to-java: Modulo() -> $[ % ]
to-java: Plus() -> $[ + ]
to-java: Min() -> $[ - ]
to-java: Lt() -> $[ < ]
to-java: LtEq() -> $[ <= ]
to-java: Gt() -> $[ > ]
to-java: GtEq() -> $[ >= ]

to-java: Min(i) -> $[-[<to-java>i]]

to-java: Block(stats) -> $[[<concat-strings><map(to-java-stat)>stats]]

to-java: If(c,stats) -> $[ if ([<to-java>c]) {
		[<concat-strings><map(to-java)>stats]
}
]

to-java: IfElse(c,stats1,stats2) -> $[ if ([<to-java>c]) {
		[<concat-strings><map(to-java)>stats1]
}
else {
		[<concat-strings><map(to-java)>stats2]
}
]

to-java: FieldRef(obj,f) -> $[[<to-java>obj].getVar("[f]")]

to-java: Assign(v@VarRef(_), e) -> $[[<to-java>v] = [<to-java>e];
]

to-java: VarDecl(t,n) -> $[[<to-java>t] [n]]
to-java: VarRef(v) -> $[[v]]

to-java: Assign(FieldRef(obj,field),val) -> $[[<to-java>obj].setVar("[field]",[val']);
]
where
	<to-java>val => val'

 

to-java: FuncDecl (n,params,evts,stats) -> $[ void [n] ([<concat-glue(|",")><map(to-java)>params]) {
	[<concat-strings><map(to-java-stat)>stats]	
	
}

]


to-java: Param(t,n) -> $[[<to-java>t] [n]]

	
to-java: Config() -> $[ctrl.config()]
to-java: Module() -> $[ctrl.module()]
to-java: Region() -> $[ctrl.region()]
to-java: Meta() -> $[ctrl.meta()]
to-java: Robot() -> $[ctrl.robot()]
to-java: Actuation() -> $[mfActuation]

to-java: VarRef(n) -> n
to-java: PayloadRef(n) -> n
to-java: Par(e) -> $[ ([<to-java>e]) ]

to-java: Const(n) -> n
to-java: Int(i) -> i
to-java: Bool(b) -> b

to-java: Byte() -> $[byte]
to-java: Boolean() -> $[boolean]

to-java: ByteArr() -> $[byte[brackets]] where
	brackets := "[]"

to-java: ByteArr(bytes) -> $[new byte [<bracks>]{[<concat-glue(|",")><map(to-java)>bytes]}]


to-java: NbsCount(exp) -> <to-java>exp

to-java: NbsOp(constraints) -> $[nbs()[<concat-strings><map(to-java)>constraints]]

to-java: NbsFilter(name,val) -> $[.nbsFilter[<to-upper-first>name]([<to-java>val])]

to-upper-first: str -> <conc-strings>(<implode-string>[<to-upper>head],<implode-string>tail)
where
	<explode-string> str => [head|tail]
	  

to-java: PacketRef(p) -> $[Packet[p]]


to-java: Unicast(p@PacketRef(packet),AssignList(assigns),dests,rate) -> $[
	if (ctrl.freqLimit("[packet]",[<compile-rate>rate])) {
		unicast(([p'])new [p'](this)[assigns'],[<to-java>dests]);
	}
	]
where
	p' := <to-java>p;
	assigns' := <concat-strings><map(to-java)>assigns


compile-rate: Some(Rate(exp)) -> <to-java>exp
compile-rate: None() -> $[true]

to-java: AssignElem(var,val) -> $[.setVar("[<to-java>var]",[val])]





to-java: VisItem (_,hemisphere,ref,c,_) -> $[visual.setColor("[<strip-annos>hemisphere]",[<to-java>ref],Color.[c]);
]





	
to-java: MetaPart(n) -> $[[n],]

to-java: FieldDecl(t,n) -> $[[<to-java>t] [n];
]
to-java: PayloadDecl(t,n) -> $[[<to-java>t] [n];
]




to-java:  BorderItem(dir,exp) -> $[if (d.equals([<to-java>dir])) {
	ret = [<to-java>exp];
}
]
where <debug>"Compile BorderItem"

to-java: BorderLeft() -> $[BorderLine.LEFT]
to-java: BorderRight() -> $[BorderLine.RIGHT]
to-java: BorderTop() -> $[BorderLine.TOP]
to-java: BorderBottom() -> $[BorderLine.BOTTOM] 	 

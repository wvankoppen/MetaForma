module generate

imports
	
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/MetaformaLang
  trans/constructors
  lib/editor-common.generated
  lib/compilation-library.generated
  libjava-front
  lib/editor-common.generated
  lib/index-library.generated
  lib/analysis-library.generated
  lib/analysis-library-internal.generated
  lib/compilation-library.generated
  lib/nbl-library.generated
	names
	desugar
	generate-indirect
	nblhacks
 store
 functions
strategies

 
 
 to-java: Group (g,mods) -> <map2str(to-java(|g))>mods
 
 to-java(|g): Mod(n) -> $[[g]_[n],]
 to-java: Group(n) -> $[[n](true),]  
 
 
 
rules // Transformation to java strings.




to-java-stat: exp -> $[[<to-java>exp];
]
where
	<debug(!"Compile stat: ")>exp

to-java: FuncCall(n,args) -> $[[<to-java>n]([<concat-glue(|",")><map(to-java)>args])] where <debug(!"FuncCall: ")>n
to-java: ModHolderNumRef(g,i) -> $[new Module(Mod.[g],[<to-java>i])]
to-java: ModHolderRef(g,m) -> $[Mod.[g]_[m]]
to-java: ModHolderRef(g) -> $[Group.[g]]  
to-java: FuncRef(n) -> n

to-java: OrientRef(o) -> $[Orientation.[o]]


to-java: FuncCall(FuncActuationRef(f), args) -> $[ctrl.getActuation().[f]([<concat-glue(|",")><map(to-java)>args])]

to-java: MethodRef(obj,n) -> $[[<to-java>obj].[n]]
where <debug>$[Compile methodref [n]]

to-java: Break() -> $[mfStateMngr.nextInstruction()]

to-java-instr: Instr(i,precon,evhdlrs,stat*,postcon) -> $[ 
	[instr] 
	] 
where
	<debug(!$[Instruction start [i]: ])>evhdlrs;
	<map2str(to-java-stat)>evhdlrs => java-evhdlrs;
	<debug(!$[Instruction stats [i]:])>stat*;
	<map2str(to-java-stat)>stat* => java-stat*;
	<debug(!$[Instruction stopcondition [i]:])>postcon;
	<to-java-instr-stats(|i,<conc-strings>(java-evhdlrs,java-stat*))>(precon,postcon) => instr;
	<debug>$[Instruction end [i]]

to-java-stat: EvHdlr(name,receive,vardecl,instr) -> "" where <debug(!"Java EV hdlr as stat!")>receive //  Will already be compiled by compile rule



// A doWait has no event handlers because it is only evaluated once
to-java-instr-stats(|i,java): (precon,postcon) -> 
$[ if (mfStateMngr.do[wait-or-until]([i]))  {
	[content]
	}]
where
	wait-or-until := <aterm-name>postcon;
	java-all := <conc-strings>(java,<to-java-post><aterm-content>postcon);
	content := <add-precon(|java-all)>precon

add-precon(|java): None() -> java
add-precon(|java): Wait(e) -> $[if (mfStateMngr.preCon([<to-java>e])){
			[java]
		}]


to-java-post: Consensus() -> $[mfStateMngr.postState();]

to-java-post: e -> $[mfStateMngr.spend([e']);] 
	where
		not (<?Consensus()>e);
		<to-java>e => e'




mod-enum: Mod(groupname,name) -> $[[groupname]_[name]]
mod-enum: Group(groupname) -> $[[groupname](true)]



to-java-stat: Wait(time,stats) -> $[if (ctrl.getStateMngr().waitFor([<to-java>time])) {
		[<map2str(to-java-stat)>stats]}]
		
to-java: TryRegion(n,Some(Orientation(o)),incs,Some(Excluding(excls))) -> $[region().tryRegion(new byte[<bracks>]{[<concat-glue(|",")><map(to-java)>incs]},new byte[<bracks>]{[<concat-glue(|",")><map(to-java)>excls]},[<to-java>n],[<to-java>o])]
to-java: TryRegion(n,None(),incs,Some(Excluding(excls))) -> $[region().tryRegion(new byte[<bracks>]{[<concat-glue(|",")><map(to-java)>incs]},new byte[<bracks>]{[<concat-glue(|",")><map(to-java)>excls]},[<to-java>n])]

to-java: OrientMatch(orients) -> $[[<concat-glue(|" || ")><map(orientref)>orients]
] 

orientref: OrientRef(r) -> $[mfStateMngr.getState().getOrientation().equals(Orientation.[r])]

to-java: Orientation(o) -> <to-java>o

to-java: ExecuteOrient(s,orient) -> $[mfStateMngr.setNextSequence([<to-java>s],[<to-java>orient])]
to-java: Execute(s) -> $[mfStateMngr.setNextSequence([<to-java>s])]

// to-java-stat: Strategy(strats) -> <map2str(to-java-stat)>strats

to-java: SequenceRef(n) -> $[Sequence.[n]] 
to-java: SequencePrimRef(n) -> $[SequenceGeneric.[n]]  


to-java: BinOp (op,e1,e2) -> $[([<to-java>e1] [<to-java>op] [<to-java>e2])]
to-java: BoolOp (op,e1,e2) -> $[([<to-java>e1] [<to-java>op] [<to-java>e2])]
 where <debug>"Compile BoolOp"

to-java: AndBit() -> $[ & ]
to-java: OrBit() -> $[ | ]

to-java: And() -> $[ && ]
to-java: Or() -> $[ || ]
to-java: Eq() -> $[ == ]
to-java: Neq() -> $[ != ]
to-java: Modulo() -> $[ % ]
to-java: Plus() -> $[ + ]
to-java: Mul() -> $[ * ]
to-java: Div() -> $[ / ]
to-java: Min() -> $[ - ]
to-java: Lt() -> $[ < ]
to-java: LtEq() -> $[ <= ]
to-java: Gt() -> $[ > ]
to-java: GtEq() -> $[ >= ]

to-java: Not(exp) -> $[ !([<to-java>exp])]

to-java: Min(i) -> $[-[<to-java>i]]

// to-java: Block(stats) -> $[[<map2str(to-java-stat)>stats]] blocks are out-desugared 


// to-java-stat: Ifs(ifs) -> <map2str(to-java-stat)>ifs

to-java-stat: If(c,stats) -> $[ if ([<to-java>c]) {
		[<map2str(to-java-stat)>stats]
}]
where <debug>$[Compile IF start]

to-java-stat: IfElse(c,stats1,stats2) -> $[ if ([<to-java>c]) {
		[<map2str(to-java-stat)>stats1]
}
else {
		[<map2str(to-java-stat)>stats2]
}]
where <debug>$[Compile IFELSE start]

// to-java: FieldRef(obj,f) -> $[[<to-java>obj].getVar("[f]")]
to-java: d@FieldRef(obj,f) -> $[[<to-java>obj].[f]]
// where 
// 	f' := <strip-annos>f;
// 	 not (<eq>(f',"id"));
// 	 not (<eq>(f',"group"))

to-java: ConstDecl(t,n,v) -> $[public final [<to-java>t] [n] = [<to-java>v]]

to-java: FieldPrimRef(obj,f) -> $[[<to-java>obj].get[<to-let-func>f]()]



// Still used in events (and functions)!
to-java: VarDecl(t,n,v) -> $[[<to-java>t] [n]; [n]=([<to-java>t])[<to-java>v];]


// FieldRefs may be parsed as Refs, then we need to extract from URI
to-java: Assign(v@Ref(_{[Field(),name,obj,package]}), e) -> <to-java>Assign(FieldRef(obj,name),e)
to-java: Assign(v@Ref(_{uri@[Var()|_]}), e) -> $[[<to-java>v] = [<uri-to-cast>uri][<to-java>e]]


to-let-func = switch id
		case ?"id":  !"ID"
		case ?"regionID":  !"RegionIDAtStateBegin"
		otherwise: <to-upper-first><id>
	end

to-java: PayloadPrimRef(var,n) ->$[[<to-java>var].get[<to-let-func>n]()]
to-java: PayloadRef(var,n) ->$[[<to-java>var].[n]]	


to-java: Assign(FieldPrimRef(obj,f),val) -> $[[<to-java>obj].set[<to-let-func>f]([<to-java>val])]
to-java: Assign(FieldRef(obj,field),val) -> $[[<to-java>obj].setVar("[field]",[<to-java>val])]

to-java-stat: FuncDecl (t,n,params,vars,stats) -> $[ [<to-java>t] [n] ([<concat-glue(|",")><map(to-java)>params]) {
	[<map2str(to-java-stat)><conc>(vars,stats)]	
}
]

to-java: Return (e) -> $[return [<to-java>e]]
to-java: Param(t,n) -> $[[<to-java>t] [n]]
	
to-java: Config() -> $[ctrl.config()]
to-java: Module() -> $[module()] // no ctrl. prefix!
to-java: Region() -> $[ctrl.region()]
to-java: Meta() -> $[meta()]



to-java: Ref(n{[Unresolved(_)|_]}) -> <debug>"UNRESOLVED VAR!"
to-java: Ref(n{[Var()|_]}) -> $[[<strip-annos>n]]
to-java: Ref(n{[Field(),name,class,package]}) -> <to-java>FieldRef(class,n)
to-java: Ref(n{[Constant()|_]}) -> <strip-annos>n
to-java: Ref(n{[MetaPart()|_]}) -> $[MetaPart.[<strip-annos>n]]


to-java: Par(e) -> $[ ([<to-java>e]) ]

to-java: Const(n) -> $[MfController.[n]]
to-java: Null() -> $[null]
to-java: Int(i) -> i
to-java: Float(v) -> $[[v]f]
to-java: Bool(b) -> b

to-java: Int() -> $[int]
to-java: Byte() -> $[byte]
to-java: Float() -> $[float]
to-java: Boolean() -> $[boolean]
to-java: Void() -> $[void]
to-java: Packet() -> $[PacketBase]

to-java: ByteArr() -> $[byte[<bracks>]] 

to-java: ByteArr(bytes) -> $[new byte [<bracks>]{[<concat-glue(|",")><map(to-java)>bytes]}]


to-java: NbsCount(exp) -> $[[<to-java>exp].size()]

to-java: NbsOp(constraints) -> $[ctrl.nbs()[<map2str(to-java)>constraints]]
where <debug>$[Compile NbsOp]

to-java: NbsFilter(name,val) -> $[.nbsFilter[<to-upper-first>name]([<to-java>val])]

	 
to-java: Packet(p) -> $[Packet[p]]


to-java-stat: Unicast(p@Packet(packet),AssignList(assigns),dests) -> $[
	if (ctrl.freqLimit("[packet][<newname>]",mfConfig.getPropagationRate())) {
		ctrl.unicast(([p'])new [p'](ctrl)[assigns'],[<to-java>dests]);
	}]
where
	p' := <to-java>p;
	assigns' := <map2str(to-java)>assigns


// to-java: AssignElem(var,val) -> $[.setVar("[<to-java>var]",[<to-java>val])]
to-java: AssignElem(PayloadRef(Packet(_),var),val) -> $[.setVar("[var]",[<to-java>val])]




to-java: VisItem (_,hemisphere,ref,c) -> $[mfVisual.setColor("[<strip-annos>hemisphere]",[<to-java>ref],[<to-java>c]);
]

to-java: Color(c) -> $[Color.[c]]
to-java: ColorCust(c) -> $[Color.decode("[<make-rgb>c]")]

make-rgb: "purple" -> "#7F007F"
make-rgb: "brown" -> "#7F3F00"



	
to-java: MetaPart(n) -> $[[n],]
to-java: MetaPartNoneRef(_) -> $[MetaPart.NONE]

to-java: FieldDecl(t,n) -> $[public [<to-java>t] [n]]

to-java: PayloadDecl(t,n) -> $[public [<to-java>t] [n]]


to-java: ModuleBorderCheck(_, _, BorderArg(b)) -> $[module().atBorder([<to-java>b])]
to-java: ModuleBorderCheck(_, _, BorderArg(Orient(),prim-sec)) -> $[module().atBorder[<to-upper-first>prim-sec]()]

to-java:  BorderItem(dir,exp) -> $[if (d.equals([<to-java>dir])) {
	ret = [<to-java>exp];
}
]
where <debug>"Compile BorderItem"


to-java: Border(b) -> $[BorderLine.[<to-upper-first>b]] where <debug>$[Border [b]]

// +1 because PacketDiscover has 0, and we can't do bitwise or on that
to-debug: n -> $[pow2([n].getTypeNr() + 1)]



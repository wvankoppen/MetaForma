package Christensen

sequence Strategy {
	when (receive (Packet p)) {
		meta.neighborHook (p);
	}
	
	do {
		if (meta.type==WALKER) {
			wait (25) { // 20 = too low, needs to be high for parallel walker!
				try region LiftUp (TOP_LEFT) including Top excluding Bottom,Up;
			}
		}
		else {
			// we cant use Walker.Right to create the region, as it is hanging in the air! --> YES, but do more early
			if (module.id == @Walker.Right) {
				if (#(@X,inRegion:false)==2)
					try region LiftDown including Top;
				if (#(@X,inRegion:false)==3) 
					execute WalkStep;
			}
		}
	} until (false);
}


sequence LiftUp {
	module.storeID();
	module.gradientInit();
	do {
		module.gradientPropagate();
	}until (config.propagationTime);
	
	do {
		if (gradPri==2 && gradSec==2) module.id = @Walker.Left;
		if (gradPri==3 && gradSec==1) module.id = @Walker.Middle;
		if (gradPri==2 && gradSec==0) module.id = @Walker.Right;
		if (gradPri==1 && gradSec==1) module.id = @Lifter.Main;
		if (gradPri==0 && gradSec==0) module.id = @Lifter.Help;
	}
	do {
		disconnect(@Lifter.Main, @Lifter.Help);
		disconnect(@Lifter.Main, @Walker.Left);
	}
	rotate(@Walker.Right,QUART);
	rotate(@Lifter.Main,QUART);
	connect(@Walker.Left,@X,true);  // Also connect to modules in other region
	disconnect(@Walker.Right,@Lifter);
	do {
		rotateOrigin(@Lifter.Main);
		rotateOrigin(@Walker.Right);
	}		
	do {
		connect(@Lifter.Main,@Lifter.Help);
		rotate(@Walker.Middle,-EIGHT);
	}
	rotate(@Walker.Left,HALF);
	rotate(@Walker.Middle,EIGHT);
	if(module.metaID == meta.regionID) {
		meta.type = SCAFFOLD;
		disable region; // We need keep the meta-module
	}
	else {
		module.restoreID();
		disable meta; // Also disable the meta walker
	}
}

sequence WalkStep {
	connect(@Walker.Right, @X);
	disconnect(@Walker.Left, @X,true); // Also disconnect modules in other region
	rotate(@Walker.Middle,-EIGHT);
	rotate(@Walker.Right,-HALF);
	rotate(@Walker.Middle, EIGHT);
	if (module.id == @Walker.Left)		module.id = @Walker.Right;
	else if (module.id == @Walker.Right)module.id = @Walker.Left;
}

sequence LiftDown {
	do {
		meta.absorb();
	}until(consensus());
	
	do {
		connect(@Walker.Right,@Lifter.Main);
		retractSet(@Lifter.Main, NORTH&FEMALE);	
	}
	
	disconnect(@Walker.Left,@X,true);  // Also disconnect modules in other region
	rotate(@Lifter.Main,QUART);
	rotate(@Walker.Right,QUART);
	
	connect(@Walker.Left,@Lifter.Main);
	disconnect(@Walker.Right,@Lifter.Main);
	do {
		rotateOrigin(@Lifter.Main);
		rotateOrigin(@Walker.Right);
	}
	do {
		connect(@Walker.Right,@Lifter.Main);
		connect(@Lifter.Main,@X);
	}
	
	module.fixSymmetry (module.metaID != meta.regionID && module.atBorder(BOTTOM));
	disable meta;
}
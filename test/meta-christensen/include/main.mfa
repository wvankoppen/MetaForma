package Christensen

debug {
	show [AssignMetaID,AddNeighbor,MetaVarSync,Gradient,Packet,Discover,Absorb,Region]
	
	visualize {  
	 	north {
	 		@X: blue;
	 		@Walker: cyan;
	 		@Uplifter: white; 
	 	}
	 	
	 	south { 
	 		Main: red; 
	 		Choose: yellow;
	 		GetUp: green;
	 		WalkStep: brown;
	 		GetDown: purple; 	 
	 	} 
	} 
}

int HALF = 180
int QUART = 90
int EIGHT = 45

int DELAY_GETUP = 5

group Walker[Left,Top,Right]
group Uplifter[Left]

packet AssignMetaID {
	byte newID;
	byte index;
}
  
packet AddNeighbor {
	byte first;
	byte second;
}

packet Gradient {
	byte pri;
	byte sec;
} 

packet Absorb {
	
}

when (receive (AddNeighbor p)) {
	if (p.metaID != 0) {
		if (p.metaID == meta.Left) {
			meta.TopLeft = p.first;  
			meta.BottomLeft = p.second;
		}
		if (p.metaID == meta.Right) {
			meta.TopRight = p.first;
			meta.BottomRight = p.second;
		}
		if (p.metaID == meta.Top) {
			meta.TopLeft = p.first;
			meta.TopRight = p.second;
		}
		if (p.metaID == meta.Bottom) {
			meta.BottomLeft = p.first;
			meta.BottomRight = p.second;
		}
	}
}

when (receive (Absorb p)) {
	if (module.metaID == 0) {
		module.metaID = p.metaID;
		module.backupID();
		module.id = @Uplifter.Left; 
	}
}
 
 
when (receive (Gradient p)) {
	if (p.pri + 1 < module.gradPri || p.sec + 1 < module.gradSec) {
		module.gradPri = min (p.pri + 1,module.gradPri);
		module.gradSec = min (p.sec + 1,module.gradSec);
		module.gradientPropagate();
	}	
}

region {
	borders [
		LEFT: #(connSource: EAST&MALE)>0; 
		RIGHT: #(connSource: WEST&MALE)>0;
		TOP: #(connSource: WEST&FEMALE)>0 && #(connSource: EAST&FEMALE)==0;
		BOTTOM: #(connSource: NORTH&MALE)==1 && #()==1; // #(connSource: EAST&FEMALE)>0 && #(connSource: WEST&FEMALE)==0;
	]
}

meta {
 	part [LeftPart, TopPart, RightPart];	
	// Future work: express meta.Right as Right 
	byte Top;
	byte Bottom;
	byte Left;
	byte Right;
	byte TopLeft;
	byte TopRight;
	byte BottomLeft; 
	byte BottomRight;
	byte Up;

	byte isStanding;

	void neighborHook (Packet p) {
		if (p.metaID != module.metaID && module.metaID != 0 && p.metaID != 0 && p.sourceGroup != @Walker) { // Walker not needs to be compromised
			if (!isStanding() || (module.id != @Walker.Left)) { // && isWEST(p.connSource)
				if (module.metaPart == TopPart) {
					Top = p.metaID;
				}
				
				if (module.metaPart == RightPart) {
					Bottom = p.metaID;
				}
				
				if (module.metaPart == LeftPart) {
					if (is(NORTH,p.connDest)) {
						Left = p.metaID;
					}
					else {
						if (is(WEST,p.connDest)) {
							BottomLeft = p.metaID;
						}
						else {
							Bottom = p.metaID;
						}
					}
				}
			}
		}
	}
 		
	void absorb() {
		if (module.id == @Walker.Right) { // Otherwise we might capture modules intended for meta-module
			unicast Absorb() to $(inRegion:false);
			meta.sizeExtra = 1;
		}
	}
	
	boolean isStanding() {
		return isStanding == 1;
	}
	
	void setStanding (boolean standing) {
		if (standing) {
			isStanding = 1;
		}
		else {
			isStanding = 0;
		}
	}
 	
 }

 

module  { 
	byte gradPri;
	byte gradSec;
	boolean isRef;
	boolean sourcePri;
	boolean sourceSec;
	

	
	void gradientPropagate () {		 
		if (sourcePri){
			gradPri = 0;
		} 
		
		if (sourceSec) {
			gradSec = 0;
		}
		  unicast Gradient(pri=gradPri, sec=gradSec) to $();
	}
 		

	void gradientInit() {
		sourcePri = region.atPrimaryBorder();
		sourceSec = region.atSecondaryBorder();
		
		gradPri = MAX_BYTE;
		gradSec = MAX_BYTE;			
	}
 }

  
sequence Main {
 	// During a sequence we should not add meta neighbors as the connector numbers can be swapped! So only do this at DEFAULT or INIT operation state
	when (receive (Packet p)) {
		meta.neighborHook (p);
	}
	
	
	 
	do {	  
		when (receive (AssignMetaID p)) {
			byte index;
						
			module.metaID = p.newID;
				
			if (p.index == 0) {
				module.metaPart = TopPart; 
				index = 1;
			}
			else if (p.index == 1) {
				module.metaPart = RightPart;
				meta.enable(); 
				execute Choose;
			}
			
			unicast AssignMetaID (newID = p.newID,index=index) to $(connSource: pow2(mirror(p.connDest,true)));
		}
		when (module.metaID == 0 && #(connSource: EAST&MALE&NORTH, metaPart:None)>0 && #(connSource: WEST&MALE&NORTH, metaID:0)==0) {
			module.initMeta(); 
			module.metaPart = LeftPart;
		}	
		// When a neighbor receives a packet, it sets its metaPart variable, and thus the above event will no longer trigger. Therefore use 2 events
		when (module.metaPart == LeftPart && module.metaID != 0) {
			unicast AssignMetaID (newID = module.metaID) to $(connSource: EAST&MALE&NORTH);
		} 
		
	} until (false); // No consensus here, as the final module will jump to choose when the meta-module is completed  
	
}


sequence Choose {
	when (receive (Packet p)) { 
		// During a sequence we should not add meta neighbors as the connector numbers can be swapped! So only do this at DEFAULT or INIT operation state
		meta.neighborHook (p);
	} 

	do {
		strategy;
	} until (false);
}